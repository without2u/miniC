package yal.analyse ;

import java.util.*;
import yal.arbre.* ;
import yal.table.*;
import yal.arbre.expressions.* ;
import yal.arbre.instructions.* ;
import yal.exceptions.AnalyseSyntaxiqueException;
import yal.arbre.expressions.unaire.NonLogique;
import yal.arbre.expressions.unaire.MoinsUnaire;
import yal.arbre.expressions.binaire.arithmetique.Division;
import yal.arbre.expressions.binaire.arithmetique.Moins;
import yal.arbre.expressions.binaire.arithmetique.Multiplication;
import yal.arbre.expressions.binaire.arithmetique.Plus;
import yal.arbre.expressions.binaire.comparaison.Different;
import yal.arbre.expressions.binaire.comparaison.Egal;
import yal.arbre.expressions.binaire.comparaison.Inferieur;
import yal.arbre.expressions.binaire.comparaison.Superieur;
import yal.arbre.expressions.binaire.logique.EtLogique;
import yal.arbre.expressions.binaire.logique.OuLogique;

import java_cup.runtime.*;

action code {:
             :} ;

parser code {:

    public void report_error(String message, Object info) {

        HashMap<Integer, String> lesTerminaux = new HashMap<>() ;

        lesTerminaux.put(new Integer(CodesLexicaux.DEBUT), "debut") ;
        lesTerminaux.put(new Integer(CodesLexicaux.FIN), "fin") ;
        lesTerminaux.put(new Integer(CodesLexicaux.POINTVIRGULE), ";") ;

        StringBuffer m = new StringBuffer() ;

        if (info instanceof java_cup.runtime.Symbol) {
            java_cup.runtime.Symbol s = ((java_cup.runtime.Symbol) info);

            if (s.left >= 0) {
                m.append("\tligne : " + (s.left + 1)) ;
                if (s.right >= 0)
                    m.append(" colonne : " + (s.right+1)) ;
            }

            if (s.value != null) {
                lesTerminaux.put(CodesLexicaux.CSTENTIERE, "" + s.value) ;
            }

            if (lesTerminaux.containsKey(new Integer(s.sym))) {
                m.append(" dernier token lu : " + lesTerminaux.get(new Integer(s.sym))) ;
            }
            else {
                m.append(" expression non termin√©e") ;
            }

        }
        throw new AnalyseSyntaxiqueException("" + m) ;
    }

    public void report_fatal_error(String message, Object info) {
        report_error(message, info);
    }
:};

/* ------------Declaration of Terminals and Non Terminals Section----------- */

/* Terminals (tokens returned by the scanner).  */

terminal Symbol    PROGRAMME, DEBUT, FIN, POINTVIRGULE, LIRE, EGAL, ENTIER, ECRIRE, SI, ALORS, SINON, FINSI, NON;
terminal String    IDF, CSTENTIERE, TYPE,CSTBOOLEAN;
terminal Symbol    MOINS, SUP, INF, EGALEGAL, DIFF, ET, OU, PLUS, MULT, DIV;

/* Non terminals used in the grammar section.  */

non terminal ArbreAbstrait PROG;
non terminal Expression EXP, CST ;
non terminal BlocDInstructions LINST, LDECL;
non terminal Identificateur DECL, DECL_VAR;
non terminal Instruction AFFECT, INST, LECTURE, CONDITION;

precedence left NON, MOINS, SUP, INF, EGALEGAL, DIFF, ET, OU, PLUS, MULT, DIV;

/* ----------------------------Grammar Section-------------------- */

start with PROG ;

PROG    ::=     PROGRAMME  IDF:idf  DEBUT  LINST:li  FIN
                {:
                    Programme p = new Programme(idf,idfleft);
                    p.ajouterBloc(li);
                    RESULT = p;
                :}

        |
                PROGRAMME IDF:idf DEBUT LDECL:ld LINST:li FIN
                {:
                    Programme p = new Programme(idf,idfleft);
                    p.ajouterBloc(ld);
                    p.ajouterBloc(li);
                    RESULT = p;
                :}
        ;

LINST   ::=     LINST:li  INST:i
                {:
                    ((BlocDInstructions)li).ajouter(i) ;
                     RESULT = li ; :}
        |
                INST:i
                {:
                    BlocDInstructions b = new BlocDInstructions(ileft + 1) ;
                    b.ajouter(i) ;
                    RESULT = b ;
                :}
        ;

INST    ::=     ECRIRE EXP:e POINTVIRGULE
                {:
                    RESULT = new Ecrire(e, eleft + 1) ;
                :}

        |
                LECTURE:l
                {:
                    RESULT = l ;
                :}

        |
                AFFECT:aff
                {:
                    RESULT = aff;
                :}
        |
        		CONDITION:co
        		{:
        		    RESULT = co;
        		:}

        ;

EXP    ::=      CST:c
                {:
                    RESULT = c ;
                :}
        |
                IDF:idf
                {:
                    RESULT = new Identificateur(idf, idfleft+1);
                :}
        |
                NON EXP:ne
                {:
                    RESULT = new NonLogique(ne);
                :}
        |
                MOINS  EXP:me
                {:
                    RESULT = new MoinsUnaire(me);
                :}
        |
                EXP:e1  PLUS  EXP:e2
                {: RESULT = new Plus(e1,e2) ; :}
        |
                EXP:e1  MOINS  EXP:e2
                {: RESULT = new Moins(e1,e2); :}
        |
                EXP:e1  MULT  EXP:e2
                {: RESULT = new Multiplication(e1,e2); :}
        |
                EXP:e1  DIV  EXP:e2
                {: RESULT = new Division(e1,e2); :}
        |
                EXP:e1  INF  EXP:e2
                {: RESULT = new Inferieur(e1,e2); :}
        |
                EXP:e1  SUP  EXP:e2
                {: RESULT = new Superieur(e1,e2); :}
        |
                EXP:e1  EGALEGAL  EXP:e2
                {: RESULT = new Egal(e1,e2); :}
        |
                EXP:e1  DIFF  EXP:e2
                {: RESULT = new Different(e1,e2); :}
        |
                EXP:e1  ET  EXP:e2
                {: RESULT = new EtLogique(e1,e2); :}
        |
                EXP:e1  OU  EXP:e2
                {: RESULT = new OuLogique(e1,e2); :}
        ;

CST     ::=     CSTENTIERE:e
                {:
                    RESULT = new ConstanteEntiere(e, eleft+1) ;
                :}
        |
                CSTBOOLEAN:b
                {:
                    RESULT = new ConstanteBoolean(b, bleft+1) ;
                :}
        ;


AFFECT		    ::=	    IDF:idf EGAL EXP:e POINTVIRGULE
					{:
					    Identificateur i = new Identificateur(idf, idfleft+1);
					    Instruction aff = new AffectationVariable(i, e, idfleft+1);
					    RESULT = aff;
					:}

			    ;

LDECL		    ::=		LDECL:ld DECL:d
					{:
                        ld.ajouter(d);
                        RESULT = ld;
					:}
			    |
					DECL:d
						{:	BlocDInstructions bloc = new BlocDInstructions(dleft+1);
							bloc.ajouter(d);
							RESULT = bloc;
						:}
			    ;


DECL		    ::=		DECL_VAR:dv
						{: RESULT = dv; :}
			    ;


DECL_VAR		::=		ENTIER IDF:idf POINTVIRGULE
						{: 	Entree e = new Entree(idf);
							Type type = Type.ENTIER;
							Symbole s = new Symbole(type);
							TDS.getInstance().ajouter(e,s,idfleft+1);
							Identificateur id = new Identificateur(idf,idfleft);
							RESULT = id;
						:}
				;

LECTURE         ::=     LIRE IDF:idf POINTVIRGULE
                        {:  Identificateur id = new Identificateur(idf, idfleft + 1);
                            Instruction l = new Lire(id, idfleft + 1);
                            RESULT = l;
                        :}
                ;

CONDITION		::=		SI EXP:e ALORS FINSI
                          {:
                              RESULT = new Condition(e,eleft+1);
                          :}
                 |
                        SI EXP:e ALORS LINST:li FINSI
                        {:
                            Instruction instruction = new Condition(e,eleft+1);
                            ((Condition)instruction).addConditionAlors(li);
                            RESULT = instruction;
                        :}
                 |
                        SI EXP:e ALORS SINON LINST:li FINSI
                        {:
                            Instruction instruction = new Condition(e,eleft+1);
                            ((Condition)instruction).addConditionSinon(li);
                            RESULT = instruction;
                        :}
                |
                        SI EXP:e ALORS LINST:li1 SINON LINST:li2 FINSI
                        {:
                            Instruction instruction= new Condition(e,eleft+1);
                            ((Condition)instruction).addConditionAlors(li1);
                            ((Condition)instruction).addConditionSinon(li2);
                            RESULT = instruction;
                        :}

                ;